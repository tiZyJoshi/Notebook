{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "The Domain\n",
        "\n",
        "The main goal is to generate a Matchcard. Sample Matchcards can be found in the Excel Sheet. A Team's Matchcard contains these informations:\n",
        "* The Treasury of the team before a game\n",
        "* The Winnings of the team in the match\n",
        "* The upkeep, which is depending on whether or not players are Starplayers and how high the initial cost of the starplayer was\n",
        "* The future treasury, which is the treasury before the game + winnings - upkeep\n",
        "* Also, in case the coach has players with special roles, special bowlbot commands are generated."
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {
        "dotnet_interactive": {
          "language": "fsharp"
        }
      },
      "source": [
        "type Currency = Currency of int\n",
        "\n",
        "module Currency =\n",
        "    let sum cs = \n",
        "        Currency(List.sumBy (fun (Currency c) -> c) cs)\n",
        "\n",
        "type Position =\n",
        "    | Shaman\n",
        "    | PlayerCoach\n",
        "    | Cheerleader\n",
        "    | Filcher\n",
        "    | BribedRef\n",
        "    | Fan\n",
        "    | SquigTamer\n",
        "\n",
        "module Position =\n",
        "    let convert s = \n",
        "        match s with\n",
        "        | \"Shaman\" -> Some Shaman\n",
        "        | \"Player-Coach\" -> Some PlayerCoach\n",
        "        | \"Cheerleader\" -> Some Cheerleader\n",
        "        | \"Filcher\" -> Some Filcher\n",
        "        | \"Bribed Ref\" -> Some BribedRef\n",
        "        | \"Fan\" -> Some Fan\n",
        "        | \"Squig Tamer\" -> Some SquigTamer\n",
        "        | _ -> None\n",
        "\n",
        "    [<Literal>] \n",
        "    let BB1D6 = \"bb1d6\"\n",
        "\n",
        "    let toBowlbotMessage p =\n",
        "        match p with\n",
        "        | Shaman -> $\"{BB1D6},1d8\"\n",
        "        | PlayerCoach -> \"bb1dX,1d15\"\n",
        "        | Cheerleader -> BB1D6\n",
        "        | Filcher -> BB1D6\n",
        "        | BribedRef -> BB1D6\n",
        "        | Fan -> BB1D6\n",
        "        | SquigTamer -> BB1D6\n",
        "\n",
        "    let toEmote p =\n",
        "        match p with\n",
        "        | Shaman -> \":shaman:\"\n",
        "        | PlayerCoach -> \":coach:\"\n",
        "        | Cheerleader -> \":cheer:\"\n",
        "        | Filcher -> \":filcher:\"\n",
        "        | BribedRef -> \":ref:\"\n",
        "        | Fan -> \":fan:\"\n",
        "        | SquigTamer -> \":tamer:\"\n",
        "\n",
        "type PositionData = {\n",
        "    Cost : Currency\n",
        "}\n",
        "\n",
        "module PositionData = \n",
        "    let create cost = {\n",
        "        Cost = cost\n",
        "    }\n",
        "\n",
        "    let costToUpkeep (Currency c) =\n",
        "        if c < 56000 then (Currency 10000)\n",
        "        elif c < 106000 then (Currency 20000)\n",
        "        elif c < 156000 then (Currency 30000)\n",
        "        elif c < 206000 then (Currency 40000)\n",
        "        elif c < 301000 then (Currency 50000)\n",
        "        else (Currency 60000)\n",
        "\n",
        "type Roster = {\n",
        "    Positions : Map<int, PositionData>\n",
        "}\n",
        "\n",
        "module Roster =\n",
        "    let create positions = {\n",
        "        Positions = positions\n",
        "    }\n",
        "\n",
        "type Player = \n",
        "    | Normal of string\n",
        "    | Star of string * Currency\n",
        "    | Special of string * Position\n",
        "\n",
        "type EmoteProvider =\n",
        "    | Position of Position\n",
        "    | Winnings\n",
        "    | Upkeep\n",
        "\n",
        "module EmoteProvider =\n",
        "    let toEmote ep =\n",
        "        match ep with\n",
        "        | Position p -> Position.toEmote p\n",
        "        | Winnings -> \":winnings:\"\n",
        "        | Upkeep -> \":upkeep:\"\n",
        "\n",
        "type FanFactor = FanFactor of int\n",
        "\n",
        "type Team = {\n",
        "    Name : string\n",
        "    Treasury : Currency\n",
        "    FanFactor : FanFactor\n",
        "    Players : Player list\n",
        "}\n",
        "\n",
        "module Team =\n",
        "    let create name treasury fanfactor players roster = {\n",
        "        Name = name\n",
        "        Treasury = treasury\n",
        "        FanFactor = fanfactor\n",
        "        Players =\n",
        "            players \n",
        "            |> List.map (fun (playerName, playerPosition, positionId) -> \n",
        "                if playerPosition.Equals(playerName) then\n",
        "                    let { Cost = cost } = \n",
        "                        match roster.Positions.TryFind positionId with\n",
        "                        | Some data -> data\n",
        "                        | None -> failwith $\"Couldnt find Position {positionId} in Team {name}\"\n",
        "                    Star (playerName, PositionData.costToUpkeep cost)\n",
        "                else\n",
        "                    match Position.convert playerPosition with\n",
        "                    | Some pos -> Special (playerName, pos)\n",
        "                    | None -> Normal playerName\n",
        "                )\n",
        "    }\n",
        "\n",
        "    let getUpkeep t =\n",
        "        t.Players \n",
        "            |> List.choose (fun p -> \n",
        "                match p with\n",
        "                | Star (_, upkeep) -> Some upkeep\n",
        "                | _ -> None)\n",
        "            |> Currency.sum\n",
        "\n",
        "type MatchResult = {\n",
        "    Winnings : Currency\n",
        "}\n",
        "\n",
        "module MatchResult =\n",
        "    let create winnings = {\n",
        "        Winnings = winnings\n",
        "    }\n",
        "\n",
        "type Match = {\n",
        "    Team1 : Team * MatchResult\n",
        "    Team2 : Team * MatchResult\n",
        "}\n",
        "\n",
        "module Match =\n",
        "    let create team1 team2 = { \n",
        "        Team1 = team1\n",
        "        Team2 = team2\n",
        "    }\n",
        "\n",
        "type MatchCard = {\n",
        "    Team : Team\n",
        "    Result : MatchResult\n",
        "}\n",
        "\n",
        "module Matchcard =\n",
        "    let create team result = {\n",
        "        Team = team\n",
        "        Result = result\n",
        "    }\n",
        "\n",
        "    let positionToMessage team position =\n",
        "        let rec getMessage position players =\n",
        "            match players with\n",
        "            | [] -> \"Not rostered\"\n",
        "            | head::tail ->\n",
        "                match head with\n",
        "                | Special (_,p) when p = position -> Position.toBowlbotMessage p\n",
        "                | _ -> getMessage position tail\n",
        "        $\"{EmoteProvider.toEmote (Position position)} {getMessage position team.Players}\"\n",
        "\n",
        "    let treasuryBeforeMatchToMessage (Currency current) (Currency winnings) =\n",
        "        $\"Treasury: {current-winnings}\"\n",
        "\n",
        "    let futureTreasuryToMessage (Currency current) (Currency upkeep) =\n",
        "        $\"Future Treasury: {current-upkeep}\"\n",
        "\n",
        "    let winningsToMessage (Currency winnings) =\n",
        "        $\"{EmoteProvider.toEmote Winnings} {winnings}\"\n",
        "\n",
        "    let upkeepToMessage (Currency upkeep) =\n",
        "        $\"{EmoteProvider.toEmote Upkeep} {upkeep}\"\n",
        "\n",
        "    let print mc delimiter =\n",
        "        let upkeep = Team.getUpkeep mc.Team\n",
        "        let p2m = positionToMessage mc.Team\n",
        "\n",
        "        let addMessage m text =\n",
        "            text + m + delimiter\n",
        "            \n",
        "        String.Empty\n",
        "        |> addMessage mc.Team.Name\n",
        "        |> addMessage \"XXXLBowl #1\"\n",
        "        |> addMessage (treasuryBeforeMatchToMessage mc.Team.Treasury mc.Result.Winnings)\n",
        "        |> addMessage (winningsToMessage mc.Result.Winnings)\n",
        "        |> addMessage (p2m BribedRef)\n",
        "        |> addMessage (p2m Fan)\n",
        "        |> addMessage (upkeepToMessage upkeep)\n",
        "        |> addMessage (futureTreasuryToMessage mc.Team.Treasury upkeep)\n",
        "        |> addMessage \"Specialist Rolls:\"\n",
        "        |> addMessage (p2m Cheerleader)\n",
        "        |> addMessage (p2m Fan)\n",
        "        |> addMessage (p2m Filcher)\n",
        "        |> addMessage (p2m PlayerCoach)\n",
        "        |> addMessage (p2m Shaman)\n",
        "        |> addMessage (p2m SquigTamer)\n",
        "\n",
        "    let printTeam t delimiter =\n",
        "        let (team, result) = t\n",
        "        let mc = create team result\n",
        "        print mc delimiter\n",
        "\n",
        "    let printMatch m delimiter newLine =\n",
        "        (printTeam m.Team1 delimiter) + newLine + (printTeam m.Team2 delimiter)"
      ],
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "The API\n",
        "https://fumbbl.com/apidoc/#"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {
        "dotnet_interactive": {
          "language": "fsharp"
        }
      },
      "source": [
        "#r \"nuget: FsHttp\"\n",
        "\n",
        "open FsHttp\n",
        "open FSharp.Data\n",
        "\n",
        "[<Literal>] \n",
        "let FumblAPI = \"https://fumbbl.com/api/\"\n",
        "\n",
        "module RosterAPI =\n",
        "    [<Literal>] \n",
        "    let Endpoint = FumblAPI + \"roster/get/\"\n",
        "    [<Literal>] \n",
        "    let Sample = Endpoint + \"4950\"\n",
        "    type Data = JsonProvider<Sample>\n",
        "\n",
        "    let get (i:int) =\n",
        "        http {\n",
        "            GET $\"{Endpoint}{i}\"\n",
        "        }\n",
        "        |> toText\n",
        "        |> Data.Parse\n",
        "        |> fun r ->\n",
        "            let positions = \n",
        "                r.Positions\n",
        "                |> Array.toList\n",
        "                |> List.map (fun p -> (p.Id, PositionData.create (Currency p.Cost)))\n",
        "                |> Map.ofList\n",
        "            Roster.create positions\n",
        "\n",
        "module TeamAPI =\n",
        "    [<Literal>] \n",
        "    let Endpoint = FumblAPI + \"team/get/\"\n",
        "    [<Literal>] \n",
        "    let Sample = Endpoint + \"997349\"\n",
        "    type Data = JsonProvider<Sample>\n",
        "\n",
        "    let get (i:int) =\n",
        "        http {\n",
        "            GET $\"{Endpoint}{i}\"\n",
        "        }\n",
        "        |> toText\n",
        "        |> Data.Parse\n",
        "        |> fun t -> \n",
        "            let name = t.Name\n",
        "            let treasury = Currency t.Treasury\n",
        "            let fanFactor = FanFactor t.FanFactor\n",
        "            let players = \n",
        "                t.Players \n",
        "                |> Array.toList \n",
        "                |> List.map (fun p -> (p.Name, p.Position, p.PositionId))\n",
        "            let roster = RosterAPI.get t.Roster.Id\n",
        "            Team.create name treasury fanFactor players roster\n",
        "\n",
        "module MatchAPI =\n",
        "    [<Literal>] \n",
        "    let Endpoint = FumblAPI + \"match/get/\"\n",
        "    [<Literal>] \n",
        "    let Sample = Endpoint + \"4284928\"\n",
        "    type Data = JsonProvider<Sample>\n",
        "\n",
        "    let get (i:int) = \n",
        "        http {\n",
        "            GET $\"{Endpoint}{i}\"\n",
        "        }\n",
        "        |> toText\n",
        "        |> Data.Parse\n",
        "        |> fun m -> \n",
        "            let team1 = TeamAPI.get m.Team1.Id\n",
        "            let result1 = MatchResult.create (Currency m.Team1.Winnings)\n",
        "            let team2 = TeamAPI.get m.Team2.Id\n",
        "            let result2 = MatchResult.create (Currency m.Team2.Winnings)\n",
        "            Match.create (team1, result1) (team2, result2)"
      ],
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "To get the required data to create MatchCards for a Match first, load the match with \n",
        "```\n",
        "MatchAPI.get {MatchID}\n",
        "```"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {
        "dotnet_interactive": {
          "language": "fsharp"
        }
      },
      "source": [
        "let matchData = MatchAPI.get 4284928"
      ],
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Then create and ouput the Matchcard Data with\n",
        "```\n",
        "Matchcard.printMatch {matchData} {delimiter} {newLine}\n",
        "```\n",
        "We use NewLine as the delimiter which we get with\n",
        "```\n",
        "System.Environment.NewLine\n",
        "```"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {
        "dotnet_interactive": {
          "language": "fsharp"
        }
      },
      "source": [
        "let newLine = System.Environment.NewLine\n",
        "let delimiter = newLine"
      ],
      "outputs": []
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {
        "dotnet_interactive": {
          "language": "fsharp"
        }
      },
      "source": [
        "Matchcard.printMatch matchData delimiter newLine"
      ],
      "outputs": [
        {
          "output_type": "execute_result",
          "data": {
            "text/plain": "Bogan Picnic Hamper Stealers\r\nXXXLBowl #1\r\nTreasury: 144000\r\n:winnings: 60000\r\n:ref: Not rostered\r\n:fan: Not rostered\r\n:upkeep: 0\r\nFuture Treasury: 204000\r\nSpecialist Rolls:\r\n:cheer: Not rostered\r\n:fan: Not rostered\r\n:filcher: bb1d6\r\n:coach: Not rostered\r\n:shaman: Not rostered\r\n:tamer: Not rostered\r\n\r\nBlack Tooth Rips\r\nXXXLBowl #1\r\nTreasury: 11000\r\n:winnings: 70000\r\n:ref: bb1d6\r\n:fan: bb1d6\r\n:upkeep: 70000\r\nFuture Treasury: 11000\r\nSpecialist Rolls:\r\n:cheer: bb1d6\r\n:fan: bb1d6\r\n:filcher: bb1d6\r\n:coach: Not rostered\r\n:shaman: bb1d6,1d8\r\n:tamer: Not rostered\r\n"
          },
          "execution_count": 1,
          "metadata": {}
        }
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": ".NET (C#)",
      "language": "C#",
      "name": ".net-csharp"
    },
    "language_info": {
      "file_extension": ".cs",
      "mimetype": "text/x-csharp",
      "name": "C#",
      "pygments_lexer": "csharp",
      "version": "8.0"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}